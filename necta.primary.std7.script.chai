/**
 * Copyright 2022-2023 , MagabeLab (Tanzania). All Rights Reserved.
 * Author Edwin Magabe    edyma24@gmail.com
 */

//------------------

def getAppPackageName() {
   try {
      return getPackageName();
   } catch {
      return "com.magabe.n3cta.matokeo.ya.darasa.la.saba";
   }
}

def getScriptRunnerVersion() {
   try {
      return scriptRunnerVersion();
   } catch {
      return 1;
   }
}

def packageNameToUrl(string packageName) {
   return "https://play.google.com/store/apps/details?id=".append(packageName);
}

//------------------

global MINI_APP_VERSION_THAT_CAN_SHOW_IMAGES = 4.0

def getVersionNameAsDouble() {
   return to_double(getVersionName(getAppPackageName()));
}

//------------------

def boolToString(bool b) {
   return b ? "true" : "false";
}

//------------------


def saveOrUpdateData(string key, string value) {
   if (!saveData(key, value)) {
      return updateData(key, value);
   } else {
      return true;
   }
}

//------------------

def openTag(string tag) {
   return chaR('<').seq(spaceOptional()).seq(stR(tag, false)).seq(spaceOptional()).seq(chaR('>'));
}

def closeTag(string tag) {
   return chaR('<').seq(spaceOptional()).seq(chaR('/')).seq(spaceOptional()).seq(stR(tag, false)).seq(spaceOptional()).seq(chaR('>'));
}

//------------------

class Tag {
   def Tag(bool valid, int index, int length, string value) {
      this.valid = valid;
      this.index = index;
      this.length = length;
      this.value = value;
   }
   var valid;
   var index;
   var length;
   var value;
};

//---------------------- string extensions ------------------

def string::indexOf(string s, int start, bool caseSensitive) {
   return strIndexOf(this, s, start, caseSensitive);
}

def string::contains(string s, bool caseSensitive) {
   return strContains(this, s, caseSensitive);
}

def string::remove(string s, bool caseSensitive) {
   return strRemove(this, s, caseSensitive);
}

def string::right(int n) {
   return strRight(this, n);
}

def string::left(int n) {
   return strLeft(this, n);
}

def string::mid(int position, int n) {
   return strMid(this, position, n);
}

def string::length() {
   return strLength(this);
}

def string::replace(string before, string after, bool caseSensitive) {
   return strReplace(this, before, after, caseSensitive);
}

def string::replaceAtPosition(int position, int n, string after) {
   return strReplaceAtPosition(this, position, n, after);
}

def string::startsWith(string str, bool caseSensitive) {
   return strStartsWith(this, str, caseSensitive);
}

def string::endsWith(string str, bool caseSensitive) {
   return strEndsWith(this, str, caseSensitive);
}

def string::truncate(int position) {
   return strTruncate(this, position);
}

def string::compare(string other, bool caseSensitive) {
   return strCompare(this, other, caseSensitive);
}

def string::split(string sep) {
   return strSplit(this, sep);
}

def string::append(string str) {
   return strAppend(this, str);
}

def string::prepend(string str) {
   return strPrepend(this, str);
}

def string::isLower() {
   return strIsLower(this);
}

def string::isUpper() {
   return strIsUpper(this);
}

def string::isEmpty() {
   return strIsEmpty(this);
}

def string::simplified() {
   return strSimplified(this);
}

def string::removeAtPosition(int position, int length) {
   return strRemoveAtPosition(this, position, length);
}

def string::getTag(string t, int start) {
   var closeTag = "</".append(t).append(">");
   var tag = Tag(false, -1, -1, "");
   var len = closeTag.length();
   var firstIndex = this.indexOf("<".append(t), start, false);
   if (firstIndex != -1) {
      var lastIndex = this.indexOf(closeTag, firstIndex + 1, false);
      if (lastIndex != -1) {
         var n = (lastIndex - firstIndex) + len;
         var value = this.mid(firstIndex, n);;
         tag = Tag(true, firstIndex, n, value);
      }
   }
   return tag;
}

def string::removeOpenTag(string t) {
   var firstIndex = this.indexOf("<".append(t), 0, false);
   if (firstIndex != -1) {
      var lastIndex = this.indexOf(">", firstIndex + 1, false);
      if (lastIndex != -1) {
         var len = (lastIndex - firstIndex) + 1;
         this = this.removeAtPosition(firstIndex, len);
      }
   }
}

// Only use this function on a small atring
def string::removeCloseTag(string t) {
   this = closeTag(t).removeFrom(this, 0, -1);
}

def string::removeRegex(string regex) {
   this = strRemoveRegex(this, regex);
}

def string::replaceRegex(string regex, string replacement) {
   this = strReplaceRegex(this, regex, replacement);
}

def string::equals(string other, bool caseSensitive) {
   return this.length() == other.length() && strContains(this, other, caseSensitive);
}

//---------------------- / string extensions ------------------


//------------------

def getLocale() {
   var locale = getData("locale");
   return locale.isEmpty() ? "en" : locale;
}

def setLocale(string locale) {
   saveOrUpdateData("locale", locale);
}

def onChangeLocaleCommand(cmd, argv, argc) {
   if (cmd.equals("setLocale", false)) {
      if (argc == 1) {
         var locale = argv[0];
         setLocale(locale);
         if (locale == "sw") {
            requestCustomDialog(421, "fa::gear", "<font color='#4dff12'>MPANGILIO UMEBADILISHWA</font>", "Sasa majina ya shule yatasomeka kwa lugha ya <font color='#4dff12'><u>Kiswahili</u></font>", "Sawa", "", "#", 0.8, 0.2, false);
         } else {
            requestCustomDialog(421, "fa::gear", "<font color='#4dff12'>MPANGILIO UMEBADILISHWA</font>", "Sasa majina ya shule yatasomeka kwa lugha ya <font color='#4d12ff'><u>Kiingereza</u></font>", "Sawa", "", "#", 0.8, 0.2, false);
         }
      }
   }
}

//------------------

global imagesToBeSaved = [];
global savedImageIndex = 0;
global SAVE_IMAGE_FOR_OFFLINE_TIMERCODE = 2023457;

def saveImageForOffline(string imageUrl) {
   imagesToBeSaved.push_back(imageUrl);
   setTimer(SAVE_IMAGE_FOR_OFFLINE_TIMERCODE, 100);
}

def getOfflineImagePath(string imageUrl) {
   var path = getData(imageUrl);
   if (!path.isEmpty()) {
      if (fileExists(path) && (getFileSize(path) > 0)) {
         path = path.prepend("file:");
      } else {
         path = "";
      }
   }
   return path;
}

//------------------

def saveOtherAppsPageImages(string xml) {
   var fn = fun[](string url) {
      if (url.endsWith(".gif", false) || url.endsWith(".png", false) || url.endsWith(".jpg", false)) {
         saveImageForOffline(url);
      }
      return url;
   }
   var end = chaR('"').oR(chaR('\'')).oR(chaR('<'));
   stR("http", false).seq(any(end, "#<>").plus()).replaceInMapped(xml, fn, 0, -1);
}

//------------------

def setLoadingImage(string url) {
   var path = getOfflineImagePath(url).remove("file:", false);
   if (!path.isEmpty()) {
      saveOrUpdateData("images/loading.gif", path);
      loadingIndicatorImageChanged();
   } else {
      saveImageForOffline(url);
   }
}

def setDefaultLoadingImage() {
   saveOrUpdateData("images/loading.gif", "");
   loadingIndicatorImageChanged();
}

//------------------

def putAdsImpl(string xml, string imageUrl, string redirectUrl, double wf, double hf, string adLocation, string rootTagName, string childTagName, string urlAttributeName, string wrapperTagName) {
   if ((!adLocation.equals("top", false)) && (!adLocation.equals("middle", false)) && (!adLocation.equals("bottom", false))) {
      return xml;
   }

   var ad = "";
   if (!imageUrl.isEmpty()) {
      saveImageForOffline(imageUrl);
      var bgColor = "<BG_COLOR>transparent</BG_COLOR>";
      var enableImage = "<ENABLE_IMAGE>1</ENABLE_IMAGE>";
      var imageURL = "<IMAGE_URL>".append(imageUrl).append("</IMAGE_URL>");
      var imageWidthFraction = "<IMAGE_WIDTH_FRACTION>".append(to_string(wf)).append("</IMAGE_WIDTH_FRACTION>");
      var imageHeightFraction = "<IMAGE_HEIGHT_FRACTION>".append(to_string(hf)).append("</IMAGE_HEIGHT_FRACTION>");
      ad = "<".append(childTagName).append(">").append("<A ").append(urlAttributeName).append("=\"").append(redirectUrl).append("\"></A>").append(bgColor).append(enableImage).append(imageURL).append(imageWidthFraction).append(imageHeightFraction).append("</").append(childTagName).append(">");
   } else {
      return xml;
   }

   if (adLocation.equals("middle", false)) {
      var count = 0;
      var i = xml.indexOf(urlAttributeName, 0, false);
      while (i != -1) {
         ++count;
         i = xml.indexOf(urlAttributeName, i + urlAttributeName.length(), false);
         if (count == 10000) { //we dont want to freeze ui do we?
            return xml;
         }
      }

      if (count > 1) {
         var middleItem = count / 2;
         var n = 0
         var fn = fun[n, middleItem, ad](string s) {
            var replace = (n == middleItem) ? (s + ad) : s;
            ++n;
            return replace;
         };

         return html_tag(childTagName).replaceInMapped(xml, fn, 0, -1);
      }
   }
   var topLocation = adLocation.equals("top", false);
   var tag = (topLocation ? "<" : "</") + rootTagName + ">";
   var i = xml.indexOf(tag, 0, false);
   if (i != -1) {
      var startChunk = xml.left(i);
      var endChunk = xml.mid(i + tag.length(), -1);
      if (topLocation) {
         return startChunk + tag + ad + endChunk;
      } else {
         return startChunk + ad + tag + endChunk;
      }
   }

   return xml;
}

def putAdsToMenu(string xml, string imageUrl, string redirectUrl, double wf, double hf, string adLocation) {
   return putAdsImpl(xml, imageUrl, redirectUrl, wf, hf, adLocation, "NECTA_RESULTS", "RESULTS", "HREF", "");
}

def putAdsToHistoryPage(string xml, string imageUrl, string redirectUrl, double wf, double hf, string adLocation) {
   return putAdsImpl(xml, imageUrl, redirectUrl, wf, hf, adLocation, "SCHOOLS", "SCHOOL", "URL", "");
}

def putAdsToRegionPage(string xml, string imageUrl, string redirectUrl, double wf, double hf, string adLocation) {
   return putAdsImpl(xml, imageUrl, redirectUrl, wf, hf, adLocation, "DISTRICTS", "DISTRICT", "HREF", "REGION");
}

def putAdsToDistrictPage(string xml, string imageUrl, string redirectUrl, double wf, double hf, string adLocation) {
   return putAdsImpl(xml, imageUrl, redirectUrl, wf, hf, adLocation, "SCHOOLS", "SCHOOL", "HREF", "DISTRICT");
}

def putAdsToPSLEPage(string xml, string imageUrl, string redirectUrl, double wf, double hf, string adLocation) {
   return putAdsImpl(xml, imageUrl, redirectUrl, wf, hf, adLocation, "REGIONS", "REGION", "HREF", "PSLE");
}

//------------------

global EMAIL_US_REQUEST_CODE = 706033328;

def onShowAdvertiseWithUsDialogCommandForConsole(cmd, argv, argc) {
   if (cmd.equals("tangaza", false)) {
      showCustomDialogAfterXMillis(EMAIL_US_REQUEST_CODE, "fa::vcardo", " TANGAZA", "Taganza katika aplikesheni hii. Tangaza <font color='green'><u>shule</u></font>, <font color='yellow'><u>stationery</u></font>, <font color='cyan'><u>kitabu</u></font> n.k  kwa kutumia mfumo wa picha au video. Kwa maelezo zaidi tutumie barua pepe.", "Tuma", "<font color='white'>Baadae</font> ", "#emailUs", 0.8, 0.3, false,500);
   }
}

def onShowAdvertiseWithUsDialogCommand(cmd, argv, argc) {
   if (cmd.equals("showAdvertiseWithUsDialog", false)) {
      requestCustomDialog(EMAIL_US_REQUEST_CODE, "fa::vcardo", " TANGAZA", "Taganza katika aplikesheni hii. Tangaza <font color='green'><u>shule</u></font>, <font color='yellow'><u>stationery</u></font>, <font color='cyan'><u>kitabu</u></font> n.k  kwa kutumia mfumo wa picha au video. Kwa maelezo zaidi tutumie barua pepe.", "Tuma", "<font color='white'>Baadae</font> ", "#emailUs", 0.8, 0.3, false);
   }
   onShowAdvertiseWithUsDialogCommandForConsole(cmd, argv, argc);
}

//------------------

global VISIT_MAGABE_LAB_REQUEST_CODE = 320230506;

def onVisitMagabeLabCommandForConsole(cmd, argv, argc) {
   if (cmd.equals("MagabeLab", false)) {
      showCustomDialogAfterXMillis(VISIT_MAGABE_LAB_REQUEST_CODE, "fa::rocket", " TUTEMBELEE", "<font color='cyan'>Tembelea tovuti yetu.</font>", "Sawa", "<font color='white'>Baadae</font> ", "#visitMagabeLab", 0.8, 0.15, false,500);
   }
}
def onVisitMagabeLabCommand(cmd, argv, argc) {
   if (cmd.equals("visitMagabeLab", false)) {
      requestCustomDialog(VISIT_MAGABE_LAB_REQUEST_CODE, "fa::rocket", " TUTEMBELEE", "<font color='cyan'>Tembelea tovuti yetu.</font>", "Sawa", "<font color='white'>Baadae</font> ", "#visitMagabeLab", 0.8, 0.15, false);
   }
   onVisitMagabeLabCommandForConsole(cmd, argv, argc);
}

//------------------

class _VideoPlayerStatus {
   def _VideoPlayerStatus() {
      this.UnknownStatus = 0;
      this.NoMedia = 1;
      this.Loading = 2;
      this.Loaded = 3;
      this.Stalled = 4;
      this.Buffering = 5;
      this.Buffered = 6;
      this.EndOfMedia = 7;
      this.InvalidMedia = 8;
   }
   var NoMedia;
   var Loading;
   var Loaded;
   var Buffering;
   var Stalled;
   var Buffered;
   var EndOfMedia;
   var InvalidMedia;
   var UnknownStatus;
};
global VideoPlayerStatus = _VideoPlayerStatus();


class _VideoPlayerPlaybackState {
   def _VideoPlayerPlaybackState() {
      this.StoppedState = 0;
      this.PlayingState = 1;
      this.PausedState = 2;
   }
   var PlayingState;
   var PausedState;
   var StoppedState;
};
global VideoPlayerPlaybackState = _VideoPlayerPlaybackState();


class _VideoPlayerError {
   def _VideoPlayerError() {
      this.NoError = 0;
      this.ResourceError = 1;
      this.FormatError = 2;
      this.NetworkError = 3;
      this.AccessDenied = 4;
      this.ServiceMissing = 5;
   }
   var NoError;
   var ResourceError;
   var FormatError;
   var NetworkError;
   var AccessDenied;
   var ServiceMissing;
};

global VideoPlayerError = _VideoPlayerError();

//------------------

global SHOW_CONSOLE_REQUEST_CODE = 12052023;

def onShowConsoleCommand(cmd, argv, argc) {
   if (cmd.equals("showConsole", false)) {
      requestCustomDialog(SHOW_CONSOLE_REQUEST_CODE, "fa::windowmaximize", "Console", "<font color='cyan'>Weka Amri <font color='white'>[ Mfano: tangaza;]</font> :- </font>", "Tuma", "", "#showConsole", 0.98, 0.5, true);
   }
}

//------------------

def getOfflineVideoPath(string url) {
   var path = getData(url);
   if (!path.isEmpty()) {
      if (fileExists(path) && (getFileSize(path) > 0)) {
         path = path.prepend("file:");
      } else {
         path = "";
      }
   }
   return path;
}

def onPlayVideoCommand(cmd, argv, argc) {
   if (cmd.equals("playVideo", false) || cmd.equals("onyeshaVideo", false)) {
      if (argc > 0) {
         var url = argv[0];
         var path = getOfflineVideoPath(url);
         if (!path.isEmpty()) {
            playVideo(path);
         } else {
            playVideo(url);
         }
      }
   }
}

//------------------

def sleep(int millis) {
   var t1 = getTimestamp();
   while ((getTimestamp() - t1) < millis) {}
}

//------------------

def encodeData(string data) {
   encryptAndSaveToFile(data, "tmp");
   return getFileData(generateAppRootFilePath("tmp.mgb"));
}

def decodeData(string data) {
   saveFileData(data, "tmp2", "txt");
   return getDecryptedFileData(generateAppRootFilePath("tmp2.txt"));
}

//------------------

global SHOW_CUSTOM_DIALOG_TIMERCODE = 12520232;
def showCustomDialogAfterXMillis(requestCode, icon, title, description, yesButtonText, noButtonText, url, percentWidth, percentHeight, showInputText, millis) {
   saveOrUpdateData("Custom_Dialog_Request_Code", to_string(requestCode));
   saveOrUpdateData("Custom_Dialog_Icon", icon);
   saveOrUpdateData("Custom_Dialog_Request_Title", encodeData(title));
   saveOrUpdateData("Custom_Dialog_Request_Description", encodeData(description));
   saveOrUpdateData("Custom_Dialog_Request_YesButtonText", encodeData(yesButtonText));
   saveOrUpdateData("Custom_Dialog_Request_NoButtonText", encodeData(noButtonText));
   saveOrUpdateData("Custom_Dialog_Request_Url", url);
   saveOrUpdateData("Custom_Dialog_Request_PercentWidth", to_string(percentWidth));
   saveOrUpdateData("Custom_Dialog_Request_PercentHeight", to_string(percentHeight));
   saveOrUpdateData("Custom_Dialog_Request_ShowInputText", to_string(showInputText));
   setTimer(SHOW_CUSTOM_DIALOG_TIMERCODE, millis);
}

def showOperationSuccessDialog(string message) {
   showCustomDialogAfterXMillis(421, "fa::smileo", "<font color='white'>IMEFANIKIWA</font>", message, "Ok", "", "#", 0.8, 0.2, false, 500);
}

def showOperationFailedDialog(string message) {
   showCustomDialogAfterXMillis(421, "fa::close", "<font color='white'>IMESHINDWA</font>", "<font color='red'>" + message + "</font>", "Ok", "", "#", 0.8, 0.2, false, 500);
}

//------------------

def onSaveFileCommand(cmd, argv, argc) {
   if (cmd.equals("saveFile", false)  || cmd.equals("tunzaFaili", false)) {
      showLoadingIndicator();
      if (argc >= 1) {
         var url = argv[0];
         if (!url.contains("/", false)) {
            hideLoadingIndicator();
            showOperationFailedDialog("Invalid url");
            return;
         }

         var fileName = "";
         if (argc > 1) {
            fileName = argv[1];
         } else {
            var list = [];
            var fn = fun[list](string s) {
               list.push_back(s);
            };

            var arr = url.split("/");
            forEachString(arr, fn);

            fileName = list[list.size() - 1];
         }

         var path = generateAppRootFilePath(fileName);
         if (saveFile(url, path)) {
            saveOrUpdateData(url, path);
            pasteToClipboard(path);
            hideLoadingIndicator();
            showOperationSuccessDialog("<font color='green'> url: " + url + "<br>imetunzwa : " + path + "</font>");
         } else {
            hideLoadingIndicator();
            showOperationFailedDialog("<font color='red'> imeshindwa kutunza faili</font>");
         }

      }
      hideLoadingIndicator();
   }
}

//------------------

def onSaveAndPlayVideoCommand(cmd, argv, argc) {
   if (cmd.equals("saveAndPlayVideo", false) || cmd.equals("tunzaNaOnyeshaVideo", false)) {
      showLoadingIndicator();
      if (argc >= 1) {
         var url = argv[0];
         if (!url.contains("/", false)) {
            showOperationFailedDialog("Invalid url");
            hideLoadingIndicator();
            return;
         }

         var path = getOfflineVideoPath(url);
         if (!path.isEmpty()) {
            hideLoadingIndicator();
            playVideo(path);
            return;
         }

         var fileName = "";
         if (argc > 1) {
            fileName = argv[1];
         } else {
            var list = [];
            var fn = fun[list](string s) {
               list.push_back(s);
            };

            var arr = url.split("/");
            forEachString(arr, fn);

            fileName = list[list.size() - 1];
         }

         path = generateAppRootFilePath(fileName);
         if (saveFile(url, path)) {
            saveOrUpdateData(url, path);
            pasteToClipboard(path);
            hideLoadingIndicator();
            playVideo("file:" + path);
         } else {
            hideLoadingIndicator();
            showOperationFailedDialog("<font color='red'> imeshindwa kutunza faili</font>");
         }
      }
      hideLoadingIndicator();
   }
}

//------------------

def onOfflineVideoCommand(cmd, argv, argc) {
   if (cmd.equals("offlineVideo", false) || cmd.equals("videoIlipotuzwa", false)) {
      showLoadingIndicator();
      if (argc >= 1) {
         var url = argv[0];
         if (!url.contains("/", false)) {
            showOperationFailedDialog("Invalid url");
            hideLoadingIndicator();
            return;
         }

         var path = getOfflineVideoPath(url);
         pasteToClipboard(path);
         showCustomDialogAfterXMillis(421, "fa::close", "VIDEO ILIPOTUNZWA", "Imetunzwa = " + path, "Ok", "", "#", 0.8, 0.2, false, 500);
      }
      hideLoadingIndicator();
   }
}

//------------------

global adsShown = 0;
global adsInitialized = false;
global SHOW_AD_TIMERCODE = 1011001;

//------------------

def beforeHtmlJunkRemover(string html) {


}

def confirmHtmlJunkRemoval(string info) {
   if (info.contains("PREM", false)) {
      return false;
   } else {
      return true;
   }
}

def getPremNoName() {
   return "PREM NO";
}

def removeOpenTag(string output, string start, string end) {
   var firstIndex = output.indexOf(start, 0, false);
   if (firstIndex != -1) {
      var lastIndex = output.indexOf(end, firstIndex + 1, false);
      if (lastIndex != -1) {
         var len = (lastIndex - firstIndex) + 1;
         return output.removeAtPosition(firstIndex, len);
      }
   }
   return output;
}

def separateGradeSectionInSummary(string input) {
   var output = input;
   var i1 = input.indexOf("DARAJA", 0, false);
   if (i1 != -1) {
      var i2 = input.indexOf(")", i1, false);
      if (i2 != -1) {
         var gradeStr = input.mid(i1, i2 - i1 + 1);
         var i3 = gradeStr.indexOf("(", 0, false);
         if (i3 != -1) {
            var valid = (gradeStr.left(i3).remove("DARAJA", false).simplified().length() == 1);
            if (valid) {
               var tmp = gradeStr;
               tmp = tmp.replace("DARAJA", ", DARAJA :", false);
               output = output.replace(gradeStr, tmp, false);
            }

         }
      }
   }
   return output;
}

def filterHtml(string html) {
   var output = html;
   output = output.remove("MADARAJA YA UFAULU WA UJUMLA", false);
   output = removeOpenTag(output, "<table", ">");
   output = removeOpenTag(output, "<font", ">");

   var version = getScriptRunnerVersion();

   //remove all unwanted tr tags
   while (true) {
      var tag = output.getTag("tr", 0);
      var tr = tag.value;
      var isResultsTableHeader = tr.contains("SEX", false);
      if (isResultsTableHeader && (version < 6)) { //version less than 6 remove table header by default
         break;
      }
      if (tr.contains("JINSI", false) ||
         tr.contains("WASICHANA", false) ||
         tr.contains("WAVULANA", false) ||
         tr.contains("JUMLA", false) ||
         isResultsTableHeader) {
         output = output.removeAtPosition(tag.index, tag.length);
      } else {
         break;
      }
   }

   output = output.replace("&", "AND", false);
   output = separateGradeSectionInSummary(output);

   return output;
}

def getTdNodeTagNameForResults(int tdIndex, int tdCount, string tagText) {
   if (tdCount == 4) {
      switch (tdIndex) {
         case (0) {
            return "CNO";
         }
         case (1) {
            return "SEX";
         }
         case (2) {
            return "CANDIDATE_NAME";
         }
         case (3) {
            return "DETAILED_SUBJECTS";
         }
      }

   } else if (tdCount == 5) {
      switch (tdIndex) {
         case (0) {
            return "CNO";
         }
         case (1) {
            return "PREM_NO";
         }
         case (2) {
            return "SEX";
         }
         case (3) {
            return "CANDIDATE_NAME";
         }
         case (4) {
            return "DETAILED_SUBJECTS";
         }
      }
   }
   return "";
}


def updateMenu(string menu) {
   var output = menu;

   return output;
}


def getGenderWisePerformanceData(string html) {
   var json = "[";
   while (true) {
      var tag = html.getTag("tr", 0);
      if (!tag.valid) {
         break;
      }
      var tr = tag.value;
      if (tr.contains("JINSI", false) ||
         tr.contains("WASICHANA", false) ||
         tr.contains("WAVULANA", false) ||
         tr.contains("JUMLA", false)) {
         var index = 0;
         var item = "{";
         while (true) {
            var tdTag = tr.getTag("td", 0);
            if (tdTag.valid) {
               var text = tdTag.value.removeOpenTag("td").removeCloseTag("td").trim();
               item = item.append("\"_").append(to_string(index)).append("\":\"").append(text).append("\"");
               item = item.append(",");
               tr = tr.removeAtPosition(tdTag.index, tdTag.length);
            } else {
               break;
            }
            index = index + 1;
         }
         if (item.endsWith(",", true)) {
            item = item.mid(0, item.length() - 1);
         }
         item = item.append("}");
         var empty = (item.length() == 2);
         if (!empty) {
            json = json.append(item);
            json = json.append(",");
         }
         html = html.removeAtPosition(tag.index, tag.length);
      } else {
         break;
      }
   }

   if (json.endsWith(",", true)) {
      json = json.mid(0, json.length() - 1);
   }

   json = json.append("]");
   json = json.replace("JINSI", "GREDI", false);
   var empty = (json.length() == 2);
   if (empty) {
      json = "";
   }

   return json;
}

def verifyBaseUrlLastSegment(string url, string lastSegment) {
   return lastSegment;
}

def getNectaMenuUrl() {
   return "https://necta.go.tz/psle_results";
}

def getAbsoluteLinks(string html) {
   var output = html.replace("'", "\"", false);
   var pos = 0;
   var links = [];
   var i1 = output.indexOf("http", pos, false);
   while (i1 != -1) {
      var i2 = output.indexOf("\"", i1, false);
      if (i2 != -1) {
         pos = i2;
         var link = output.mid(i1, i2 - i1);
         links.push_back(link);
      } else {
         pos = i1;
      }
      i1 = output.indexOf("http", pos, false);
   }
   return links;
}

def getResultAbsoluteLinks(string html) {
   var links = getAbsoluteLinks(html);
   var resultLinks = [];
   for (var i = 0; i < links.size(); ++i) {
      var link = links[i].simplified();
      if (link.endsWith(".htm", false)) {
         resultLinks.push_back(link);
      }
   }
   return resultLinks;
}

def getNectaResultLinks(string html) {
   var links = getResultAbsoluteLinks(html);
   var menuLinks = [];
   for (var i = 0; i < links.size(); ++i) {
      var link = links[i];
      var year = getYear("", link);
      if (year != 0) {
         var yearTag = "<year>".append(to_string(year)).append("</year>");
         var aTag = "<a HREF=\"".append(link).append("\"></a>");
         var linkTag = "<link>".append(yearTag).append(aTag).append("</link>");
         menuLinks.push_back(linkTag);
      }
   }
   return menuLinks;
}

def onUrlVisited(string url) {
   if (url.startsWith("#", false)) {
      var cmd = "";
      var argv = [];
      var i = 0;
      var initVars = fun[i, cmd, argv](string s) {
         if (i == 0) {
            cmd = s.remove("#", false);
         }
         if (i > 0) {
            argv.push_back(s);
         }
         ++i;
      };
      var arr = url.split(";");
      forEachString(arr, initVars);
      onExecuteCommand(cmd, argv, argv.size());
   }
}

//argv is vector of strings whitch represents a list of command argurments
def onExecuteCommand(cmd, argv, argc) {
   onChangeLocaleCommand(cmd, argv, argc);
   onShowAdvertiseWithUsDialogCommand(cmd, argv, argc);
   onVisitMagabeLabCommand(cmd, argv, argc);
   onShowConsoleCommand(cmd, argv, argc);
   onPlayVideoCommand(cmd, argv, argc);
   onSaveFileCommand(cmd, argv, argc);
   onSaveAndPlayVideoCommand(cmd, argv, argc);
   onOfflineVideoCommand(cmd, argv, argc);
}


def onShareViaSms(string phoneNumber, string resultsShared) {

}


def getUserAgentString() {
   return "Mozilla/5.0 (Windows NT 10.0; WOW64; rv:56.0) Gecko/20100101 Firefox/56.0";
}


def allowSchoolNameInSwahili() {
   return getLocale() == "en" ? false : true;
}

//------------------

global LATEST_APP_VERSION_NAME = "4.0"; //Always change this this on every App release
global WIN64_DOWLOAD_URL = "https://www.mediafire.com/file/g17fo7ygy4ps5j1/Matokeo+La7+v4.0+WinX64+Installer.exe/file";
global DOWNLOAD_UPDATE_REQUESTCODE = 201;

def requestDownloadUpdateDialog(string description, string url) {
   requestCustomDialog(DOWNLOAD_UPDATE_REQUESTCODE, "fa::download", "Pakua toleo jipya!!", description, "Pakua", "Funga App", url, 0.8, 0.3, false);
}

def downloadUpdate() {
   var packageName = getAppPackageName();
   var versionName = getVersionName(packageName);
   if (versionName != LATEST_APP_VERSION_NAME) {
      var os = getOperatingSystem();
      var url = (os == "android") ? packageNameToUrl(packageName) : WIN64_DOWLOAD_URL;
      var message = "Toleo jipya la aplikesheni hii ambalo ni ".append(LATEST_APP_VERSION_NAME).append(" limeshatoka. Pakua na 'rate' aplikesheni hii sasa.");
      requestDownloadUpdateDialog(message, url);
   }
}

//------------------

global PAKUA_MATOKEO_KIDATO_4NA6_TIMERCODE = 32;
global FORM4_AND_6_PACKAGENAME = "com.magabelab.matokeo.kidato.cha4na6";
global PAKUA_MATOKEO_DARASA_LA_NNE_TIMERCODE = 252022;
global MATOKEO_DARASA_LA_NNE_PACKAGENAME = "com.magabelab.matokeo.ya.darasa.la.nne";
global DONATE_REQUESTCODE = 13101989;
global DONATE_TIMERCODE = 202226;

def onAppStarted() {
   try {

      downloadUpdate();

      var vn = getVersionNameAsDouble();

      if (vn == 3.8) {
         get("https://iplogger.com/1yfZf7.gif");
      }

      if (vn == 3.9) {
         get("https://iplogger.com/1yJkh7.gif");
      }

      if (vn == 4.0) {
         get("https://iplogger.com/11CwC.gif");
      }

      initializeAds();

   } catch {}

}

def onAppExited(int status) {


}

def onStudentResultsOpened(string name, string no, string schoolName, string average, string year, string results) {

}

/* info is in json format*/
def onCandidateResultsClicked(string info) {

}

global PAKUA_MATOKEO_KIDATO_4NA6_REQUESTCODE = 89;
global PAKUA_MATOKEO_DARASA_LA_NNE_REQUESTCODE = 12252022;

/* pressedButton = no or yes */
def onCustomDialogClosed(int requestCode, string pressedButton, string answer) {
   switch (requestCode) {
      case (PAKUA_MATOKEO_KIDATO_4NA6_REQUESTCODE) {
         if (pressedButton == "yes") {
            showToastMessage("Asante, Tafadhali subiri");
         } else {
            showToastMessage("Sawa ila kumbuka kupakua aplikesheni za matokeo zilizotengenezwa na Magabe Lab kutoka Google play.");
         }
         break;
      }

      case (PAKUA_MATOKEO_DARASA_LA_NNE_REQUESTCODE) {
         if (pressedButton == "yes") {
            showToastMessage("Asante, Tafadhali subiri");
         } else {
            showToastMessage("Sawa ila kumbuka kupakua aplikesheni za matokeo zilizotengenezwa na Magabe Lab kutoka Google play.");
         }
         break;
      }

      case (DOWNLOAD_UPDATE_REQUESTCODE) {
         if (pressedButton == "yes") {
            showToastMessage("Tafadhali subiri");
         } else {
            showToastMessage("Ni wamuzi mzuri kupakua toleo hili kwani ndio toleo bora."); //show sad face
            var os = getOperatingSystem();
            if (os == "android") {
               quit();
            }
         }
         break;
      }

      case (DONATE_REQUESTCODE) {
         if (pressedButton == "yes") {
            showToastMessage("Tafadhali subiri kidogo");
            openPage("https://drive.google.com/file/d/1sN0onhIxIKtF4rTkrUbojU91F7wAh6w4/view?usp=drivesdk");
         } else {
            var version = getScriptRunnerVersion();
            var tm = 1000 * 3; //3 sec
            if (version >= 4) {
               var os = getOperatingSystem();
               if (os == "android") {
                  if (!isPackageExisted(MATOKEO_DARASA_LA_NNE_PACKAGENAME)) {
                     setTimer(PAKUA_MATOKEO_DARASA_LA_NNE_TIMERCODE, tm);
                  } else if (!isPackageExisted(FORM4_AND_6_PACKAGENAME)) {
                     setTimer(PAKUA_MATOKEO_KIDATO_4NA6_TIMERCODE, tm);
                  }
               }
            }
         }

         break;
      }

      case (EMAIL_US_REQUEST_CODE) {
         if (pressedButton == "yes") {
            showToastMessage("Asante, Tafadhali subiri");
            openPage("mailto:magabelab.tz@gmail.com");
         } else {
            showToastMessage("Sawa!");
         }
         break;
      }

      case (VISIT_MAGABE_LAB_REQUEST_CODE) {
         if (pressedButton == "yes") {
            showToastMessage("Asante, Tafadhali subiri");
            openPage("https://magabelab-tz.web.app");
         } else {
            showToastMessage("Sawa!");
         }
         break;
      }

      case (SHOW_CONSOLE_REQUEST_CODE) {
         if (answer.isEmpty()) {
            return;
         }

         if (pressedButton == "yes") {
            var cmd = "";
            var argv = [];
            var i = 0;
            var initVars = fun[i, cmd, argv](string s) {
               if (i == 0) {
                  cmd = s.remove("#", false).simplified();
               }

               if (i > 0) {
                  argv.push_back(s);
               }
               ++i;
            };

            var arr = answer.split(";");
            forEachString(arr, initVars);

            onExecuteCommand(cmd, argv, argv.size());
         }
         break;
      }

      default {
         break;
      }
   }

}

def onTimeOut(int timerCode) {
   switch (timerCode) {
      case (DONATE_TIMERCODE) {
         requestCustomDialog(DONATE_REQUESTCODE, "fa::star", "MCHANGO WAKO", "Je umeipenda aplikesheni hii na ungependa kuwa mfadhili wa matengenezo na matunzo yake?", "Ndio", "Hapana", "#", 0.8, 0.2, false);
         break;
      }

      case (PAKUA_MATOKEO_KIDATO_4NA6_TIMERCODE) {
         requestCustomDialog(PAKUA_MATOKEO_KIDATO_4NA6_REQUESTCODE, "fa::download", "Matokeo kidato cha 4 na 6", "Angalia matokeo ya kidato cha 6 kwa kutumia aplikesheni hii ya kisasa", "Pakua sasa", "Badae", packageNameToUrl(FORM4_AND_6_PACKAGENAME), 0.8, 0.2, false);
         break;
      }

      case (PAKUA_MATOKEO_DARASA_LA_NNE_TIMERCODE) {
         requestCustomDialog(PAKUA_MATOKEO_DARASA_LA_NNE_REQUESTCODE, "fa::download", "Matokeo ya darasa la nne", "Sasa unaweza kuangalia matokeo ya darasa la nne kwa kutumia aplikesheni hii ya kisasa", "Pakua sasa", "Badae", packageNameToUrl(MATOKEO_DARASA_LA_NNE_PACKAGENAME), 0.8, 0.2, false);
         break;
      }

      case (SHOW_AD_TIMERCODE) {
         showOrLoadAd();
         break;
      }

      case (SAVE_IMAGE_FOR_OFFLINE_TIMERCODE) {
         if (getVersionNameAsDouble() < MINI_APP_VERSION_THAT_CAN_SHOW_IMAGES) {
            return;
         }
         if (imagesToBeSaved.empty() || savedImageIndex >= imagesToBeSaved.size()) {
            break;
         }
         var img = imagesToBeSaved[savedImageIndex];
         ++savedImageIndex;

         if (!img.isEmpty() && getOfflineImagePath(img).isEmpty()) {
            var name = "";
            var list = [];
            var fn = fun[list](string s) {
               list.push_back(s);
            };

            var arr = img.split("/");
            forEachString(arr, fn);

            name = list[list.size() - 1];

            var path = generateAppRootFilePath(name);
            if (saveImage(img, path, -1)) {
               saveOrUpdateData(img, path);
               onImageSaved(img, path);
            }
         }
         break;
      }

      case (SHOW_CUSTOM_DIALOG_TIMERCODE) {
         var requestCode = to_int(getData("Custom_Dialog_Request_Code"));
         var icon = getData("Custom_Dialog_Icon");
         var title = decodeData(getData("Custom_Dialog_Request_Title"));
         var description = decodeData(getData("Custom_Dialog_Request_Description"));
         var yesButtonText = decodeData(getData("Custom_Dialog_Request_YesButtonText"));
         var noButtonText = decodeData(getData("Custom_Dialog_Request_NoButtonText"));
         var url = getData("Custom_Dialog_Request_Url");
         var percentWidth = to_double(getData("Custom_Dialog_Request_PercentWidth"));
         var percentHeight = to_double(getData("Custom_Dialog_Request_PercentHeight"));
         var showInputText = (to_int(getData("Custom_Dialog_Request_ShowInputText")) > 0);
         requestCustomDialog(requestCode, icon, title, description, yesButtonText, noButtonText, url, percentWidth, percentHeight, showInputText);
         break;
      }

      default {
         break;
      }

   }

}

def onImageSaved(string url, string path) {
   log("Image saved: url = ".append(url).append(" , path = ").append(path));

   //if(url.endsWith("magabelab1.gif",false)){
   //   setLoadingImage(url);
   //}
}


def removeAllTags(string tag, string html) {
   var output = html;
   var open = "<".append(tag);
   var close = "</".append(tag).append(">");
   var i = output.indexOf(open, 0, false);
   while (i != -1) {
      output = removeOpenTag(output, open, ">");
      output = output.remove(close, false);
      i = output.indexOf(open, 0, false);
   }
   return output;
}

def removeColors(string html) {
   var output = html;
   var i1 = output.indexOf("\"#", 0, false);
   if (i1 != -1) {
      var i2 = output.indexOf("\"", i1 + 1, false);
      if (i2 != -1) {
         output = output.removeAtPosition(i1, i2 - i1 + 1);
      }
   }

   if (output.contains("\"#", false)) {
      output = removeColors(output);
   }
   return output;
}

def getSubjectsPerformanceData(string html) {
   var input = removeAllTags("span", html);
   input = removeColors(input);
   input = removeAllTags("p", input);
   var i1 = input.indexOf("<td >UFAULU", 0, false);
   if (i1 != -1) {
      var i2 = input.indexOf("</CANDIDATES>", 0, false);
      if (i2 != -1) {
         input = input.mid(i1, i2 - i1);
         input = input.remove("WA MASOMO KATIKA MAKUNDI YA UMAHIRI", false); //so that i can replace the apropriate tokeb
         input = input.replace("UMAHIRI</TD>", "UMAHIRI</TD></TR>", false);
      } else {
         return "";
      }
   } else {
      return "";
   }

   var json = "[";
   var trPos = 0;
   var trTag = input.getTag("tr", trPos);
   while (trTag.valid) {
      var tr = trTag.value;
      var item = "{";
      var tdPos = 0;
      var tdTag = tr.getTag("td", tdPos);
      var index = 0;
      while (tdTag.valid) {
         var td = tdTag.value;
         var text = td.removeOpenTag("td").removeCloseTag("td").trim();
         if (text.length() == 5 && text.contains("NAMBA", false)) {
            text = "#";
         }
         item = item.append("\"_").append(to_string(index)).append("\":\"").append(text).append("\"");
         item = item.append(",");

         index = index + 1;
         tdPos = tdTag.index + tdTag.length;
         tdTag = tr.getTag("td", tdPos);
      }
      if (item.endsWith(",", true)) {
         item = item.mid(0, item.length() - 1);
      }
      item = item.append("}");
      var empty = (item.length() == 2);
      if (!empty) {
         json = json.append(item);
         json = json.append(",");
      }
      trPos = trTag.index + trTag.length;
      trTag = input.getTag("tr", trPos);
   }

   if (json.endsWith(",", true)) {
      json = json.mid(0, json.length() - 1);
   }

   json = json.append("]");

   return json;
}

def getSubjectsPerformanceDialogTitle() {
   return "UFAULU WA MASOMO KATIKA MAKUNDI";
}

def getGenderWisePerformanceDialogTitle() {
   return "MADARAJA YA UFAULU WA UJUMLA";
}

def getGenderWisePerformanceDialogColumnsWidths(string url) {
   //This are fraction of width, the total must equals 1
   return "0.35, 0.13, 0.13, 0.13 , 0.13, 0.13";
}

def getSubjectsPerformanceDialogTabLable1() {
   return "WALIOFANYA";
}

def getSubjectsPerformanceDialogTabLable2() {
   return "WALIOFUTIWA";
}

def getSubjectsPerformanceDialogTabLable3() {
   return "WALIOFAULU";
}

def getSubjectsPerformanceDialogTabLable4() {
   return "WASTANI";
}


def onDisplayPSLE(string title, string url) {
   log(title.append("---").append(url));
}

def onDisplayRegion(string regionName, string title, string url) {


}

def onDisplayDistrict(string districtName, string title, string url) {

}

def onDisplaySchool(string schoolName, string title, string url) {
   if (adsShown == 0) {
      showOrLoadAd();
   }

}

def getOtherAppsPageTitle() {
   return "TAARIFA";
}

def getOtherAppsDelegateSpacing() {
   return 0;
}

def onGetNewImageUrl(string previousImageUrl) {
   return getOfflineImagePath(previousImageUrl);
}

def beforeDisplayResults(string displayName, int year, string url, string xml) {
   return xml.simplified();
}

def beforeDisplayGenderWisePerformance(string url, string json) {
   return json;
}

def beforeDisplaySubjectsPerformance(string url, string json) {
   return json;
}

def beforeDisplayMenuPage(string xml) {
   if (getVersionNameAsDouble() < MINI_APP_VERSION_THAT_CAN_SHOW_IMAGES) {
      return xml;
   }
   var output = xml;
   var w = 0.85;

   output = putAdsToMenu(output, "https://raw.githubusercontent.com/magabe26/mgb/master/tangaza3.gif", "#showAdvertiseWithUsDialog", w, 0.3, "middle");
   output = putAdsToMenu(output, "https://raw.githubusercontent.com/magabe26/mgb/master/magabelab1.gif", "#visitMagabeLab", w, 0.22, "bottom");

   return output;
}

def beforeDisplayPSLEPage(string title, string url, string xml) {
   if (getVersionNameAsDouble() < MINI_APP_VERSION_THAT_CAN_SHOW_IMAGES) {
      return xml;
   }
   var output = xml;
   var w = 0.85;

   output = putAdsToPSLEPage(output, "https://raw.githubusercontent.com/magabe26/mgb/master/tangaza2.gif", "#showAdvertiseWithUsDialog", w, 0.40, "top");
   output = putAdsToPSLEPage(output, "https://raw.githubusercontent.com/magabe26/mgb/master/magabelab1.gif", "#visitMagabeLab", w, 0.20, "bottom");

   return output;
}

def beforeDisplayRegionPage(string title, string url, string xml) {
   if (getVersionNameAsDouble() < MINI_APP_VERSION_THAT_CAN_SHOW_IMAGES) {
      return xml;
   }
   var output = xml;
   var w = 0.85;

   output = putAdsToRegionPage(output, "https://raw.githubusercontent.com/magabe26/mgb/master/tangaza2.gif", "#showAdvertiseWithUsDialog", w, 0.40, "bottom");
   output = putAdsToRegionPage(output, "https://raw.githubusercontent.com/magabe26/mgb/master/magabelab1.gif", "#visitMagabeLab", w, 0.20, "bottom");

   return output;
}

def beforeDisplayDistrictPage(string title, string url, string xml) {
   if (getVersionNameAsDouble() < MINI_APP_VERSION_THAT_CAN_SHOW_IMAGES) {
      return xml;
   }
   var output = xml;
   var w = 0.85;

   output = putAdsToDistrictPage(output, "https://raw.githubusercontent.com/magabe26/mgb/master/tangaza2.gif", "#showAdvertiseWithUsDialog", w, 0.30, "top");
   output = putAdsToDistrictPage(output, "https://raw.githubusercontent.com/magabe26/mgb/master/magabelab1.gif", "#visitMagabeLab", w, 0.20, "bottom");

   return output;
}

def beforeDisplayHistoryPage(string xml) {
   if (getVersionNameAsDouble() < MINI_APP_VERSION_THAT_CAN_SHOW_IMAGES) {
      return xml;
   }
   var output = xml;
   var w = 0.85;

   output = putAdsToHistoryPage(output, "https://raw.githubusercontent.com/magabe26/mgb/master/tangaza2.gif", "#showAdvertiseWithUsDialog", w, 0.80, "bottom");
   output = putAdsToHistoryPage(output, "https://raw.githubusercontent.com/magabe26/mgb/master/magabelab1.gif", "#visitMagabeLab", w, 0.20, "bottom");

   return output;
}

def beforeDisplayOtherAppsPage(string xml) {

   saveOtherAppsPageImages(xml);

   return xml;
}

//Ads
def initializeAds() {
   try {
      initAds("ca-app-pub-2466182993358829/9469350192");
   } catch {}
}

def onAdsInitialized() {
   //showToastMessage("Ads initialized");
   adsInitialized = true;
   loadAd();
}

def onLoadingAd() {
   //showToastMessage("Loading Ad ...");
}

def onAdLoaded() {
   //showToastMessage("Ad Loaded !");
   showAd();
}

def onFailedToLoadAd(string error) {
   //showToastMessage("Error:: Failed To LoadAd !".append(error));
}

def onAdShown() {
   //showToastMessage("Ad Shown !");
   adsShown = adsShown + 1;
   showAdAfterXMins(2);
}

def onFailedToShowAd(string error) {
   //showToastMessage("Error:: Failed To ShowAd !".append(error));
}

def onAdDismissed() {
   //showToastMessage("Ad Dismissed!");
}

def showOrLoadAd() {
   if (adsInitialized) {
      if (adLoaded()) {
         showAd();
      } else {
         loadAd();
      }
   }
}

def showAdAfterXMins(int mins) {
   var delay = 1000 * 60 * mins;
   setTimer(SHOW_AD_TIMERCODE, delay);
}

//Video
def onVideoDialogOpened(string videoUrl, string videoDescription, string redirectUrl) {
   //setVideoPlayerLoadingIndicator("https://raw.githubusercontent.com/magabe26/mgb/master/magabelab1.gif");
   //setVideoThumbnail("https://raw.githubusercontent.com/magabe26/mgb/master/magabelab1.gif")

   videoPlayerPlay();
}

def onVideoDialogClosed(string videoUrl, string videoDescription, string redirectUrl) {
   //log(" Video dialog closed"+ videoUrl);
}

def isVideoDialogCloseDisabled(string videoUrl, double position, double duration, bool errorOccured) {
   if (duration == 0) {
      return false;
   } else {
      return position / duration < 0.8;
   }
}

def isVideoSeekForwardDisabled(string videoUrl, double position, double duration) {
   return false;
}

def onVideoPlayerStatusChanged(string videoUrl, int status) {

   switch (status) {

      case (VideoPlayerStatus.NoMedia) {
         log("VideoPlayerStatus.NoMedia");

         break;
      }

      case (VideoPlayerStatus.Loading) {
         log("VideoPlayerStatus.Loading");

         break;
      }

      case (VideoPlayerStatus.Loaded) {
         log("VideoPlayerStatus.Loaded");

         break;
      }

      case (VideoPlayerStatus.Buffering) {
         log("VideoPlayerStatus.Buffering");

         break;
      }

      case (VideoPlayerStatus.Stalled) {
         log("VideoPlayerStatus.Stalled");

         break;
      }

      case (VideoPlayerStatus.Buffered) {
         log("VideoPlayerStatus.Buffered");

         break;
      }

      case (VideoPlayerStatus.EndOfMedia) {
         log("VideoPlayerStatus.EndOfMedia");

         break;
      }

      case (VideoPlayerStatus.InvalidMedia) {
         log("VideoPlayerStatus.InvalidMedia");

         break;
      }

      case (VideoPlayerStatus.UnknownStatus) {
         log("VideoPlayerStatus.UnknownStatus");

         break;
      }

      default {
         break;
      }

   }
}

def onVideoPlaybackStateChanged(string videoUrl, int playbackState) {

   switch (playbackState) {

      case (VideoPlayerPlaybackState.PlayingState) {
         log("VideoPlayerPlaybackState.PlayingState");

         break;
      }

      case (VideoPlayerPlaybackState.PausedState) {
         log("VideoPlayerPlaybackState.PausedState");

         break;
      }

      case (VideoPlayerPlaybackState.StoppedState) {
         log("VideoPlayerPlaybackState.StoppedState");

         break;
      }

      default {
         break;
      }

   }
}

def onVideoPlayerVolumeChanged(string videoUrl, double volume) {
   //log("onVideoPlayerVolumeChanged , volume = "+ to_string(volume));
}

def onVideoPlayerError(string videoUrl, int errorCode, string error) {
   closeVideoDialog();

   var errorDisplay = "";

   switch (errorCode) {

      case (VideoPlayerError.NoError) {
         errorDisplay = "VideoPlayerError.NoError";

         break;
      }

      case (VideoPlayerError.ResourceError) {
         errorDisplay = "VideoPlayerError.ResourceError";

         break;
      }

      case (VideoPlayerError.FormatError) {
         errorDisplay = "VideoPlayerError.FormatError";

         break;
      }

      case (VideoPlayerError.NetworkError) {
         errorDisplay = "VideoPlayerError.NetworkError";

         break;
      }

      case (VideoPlayerError.AccessDenied) {
         errorDisplay = "VideoPlayerError.AccessDenied";

         break;
      }

      case (VideoPlayerError.ServiceMissing) {
         errorDisplay = "VideoPlayerError.ServiceMissing";
         break;
      }

      default {
         break;
      }
   }

   requestCustomDialog(100520231, "fa::close", "<font color='red'>Imeshindwa kuonyesha</font>", errorDisplay, "Sawa", "", "#", 0.8, 0.2, false);
}


def isVideoDialogMinimizeDisabled(string videoUrl, double position, double duration, bool errorOccured) {
   if (duration == 0) {
      return false;
   } else {
      return position / duration < 0.2;
   }
}


def runTest(string input) {
   var output = input;

   return output;
}